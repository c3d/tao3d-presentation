// ----------------------------------------------------------------------------
main_title_slide "Main title",
// ----------------------------------------------------------------------------
    title "Tao3D: Like an interactive 3D LaTeX Lisp"
    subtitle
        paragraph "A presentation tool designed for developers"
        paragraph "A programming language designed for 3D animations"
    presentation_author
        paragraph "Christophe de Dinechin (dinechin@redhat.com)"

    picture
        translate 600, 500, 20
        rotate_z -8
        image 0, 0, 30%, 30%, "Tao3D.png"


// ----------------------------------------------------------------------------
base_slide "What is Tao3D?",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        text "And what is """
        italic "Interactive 3D LaTeX Lisp"
        text """ supposed to mean?"

    // We have pretty heavy animations here, control refresh precisely
    left_column
        stepping
        * "Tao3D is a programming language for real-time interactive 3D"
        ** "A programming language"
        ** "Real-time"
        ** "Interactive"
        ** "in three dimensions"
        §
        * "Like LaTeX, Tao3D is..."
        ** "Designed to create documents"
        ** "User configurable and stylable"
        §
        * "Like Lisp, Tao3D is..."
        ** "Functional"
        ** "Extensible"
        ** "Homoiconic (programs are data)"

    if smooth_step in 7.5..8.5 then
        code_position 400, 80, 700, 800
        redhat_theme "SeasonsGreetings"
    else
        code_move_default
        redhat_theme "RedHat"

    picture
        example_box 2, "Programming language", "example-1.ddd",
            import "example-1.ddd"
        example_box 3, "Real-time", "example-2.ddd",
            import "example-2.ddd"
        example_box 4, "Interactive", "example-3.ddd",
            import "example-3.ddd"
        example_box 5, "Three dimensions", "example-4.ddd",
            light 0
            light_position 100, 100, 1000
            show 1.5
            translate_z 150
            import "example-4.ddd"

        example_box 7, "Documents", "latex-1.ddd",
            image 0, -250, 30%, 30%, "Text Based.png"
        code_box_at_step 8, "Styles",
            text load_text "latex-2.ddd"

        example_box 10, "Functional", "lisp-1.ddd",
            import "lisp-1.ddd"
        example_box 11, "Extensible", "lisp-2.ddd",
            import "lisp-2.ddd"
        example_box 12, "Homoiconic", "lisp-3.ddd",
            color "darkblue"
            translate -200, -200, 0
            font "Impact", 40
            import "lisp-3.ddd"



// ============================================================================
//
section_slide "Installation",
//
// ============================================================================
    title page_label
    subtitle "Installing or building yourself..."
    picture
        translate 600, 400, 50
        rotate_z -9
        image 0, 0, 70%, 70%, "Tao3DInstall.png"


// ----------------------------------------------------------------------------
base_slide "Installing Tao3D the easy way",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        paragraph "Download from the web site and run the installer"
        paragraph { code "https://tao3d.sourceforge.net" }
    picture
        frame_texture 1500, 800,
            translate_y (650 * smooth_step - 1350) * fade_at(smooth_step, 1)
            scale 30% + 42% * fade_at(smooth_step, 1)
            image 0, 0, 100%, 100%, "Tao3DWebSite.png"
        rectangle 0, 100, 1500, 800
        at_step 3


// ----------------------------------------------------------------------------
base_slide "Building Tao3D from git",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        paragraph "Hard dependencies on Qt5 and LLVM"
        paragraph { code "https://github.com/c3d/tao-3D" }
    picture
        at_step 1
        code_box -400, 100, 700, 500, "Dependencies",
            text load_text "dependencies.txt"
        at_step 2
        code_box  400, 100, 700, 500, "Installation",
            text load_text "installation.txt"


// ============================================================================
//
section_slide "Programming Language",
//
// ============================================================================
    title page_label
    subtitle "What is different or unusual about the Tao3D language?"
    picture
        translate 500, 400, 50
        rotate_z -5
        locally
            radial_gradient 64, 64, 64, 128, 128,
                gradient_color   0%, "white", 90%
                gradient_color  70%, "white", 30%
                gradient_color 100%, "white",  0%
            ellipse 0, 0, 480, 220
        image 0, 0, 70%, 70%, "programming-languages.png"
        color "#F60", 70%
        rotate_z 25
        rectangle 450, 30


// ----------------------------------------------------------------------------
base_slide "Representing the parse tree",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Any program can be represented with only 8 node types"
    left_column
        § "A very simple parse-tree"
        §
        § "Terminal nodes in the parse tree:"
        * "Integer numbers: "; code "123"
        ** "Separators: "; code "1_000_000"
        ** "Based numbers: "; code "2#1001_0001"
        ** "Exponents (positive): "; code "1e6"
        * "Real numbers: "; code "3.14 16#F.0#e-4"
        * "Text: "; code """ABC"""; text ", "; code "'X'"
        text ", "; code "<< Hello >>"
        * "Names: "; code "foo_bar2"
        text ", symbols: "; code "++"
        §
        § "Inner nodes in the parse tree:"
        * "Prefix: ";  code "sin 42"; text ", "; code "+42"
        * "Postfix: "; code "42km"; text ", "; code "42%"
        * "Infix: "; code "42 and 1"; text ", "; code "42 + 1"
        * "Block: "; code "(42)"; text ", "; code "[42]"
        text ", "; code "{42}"; text ", indentation"

    picture
        example_box 1..16, "Parse tree", "parse-tree.ddd",
            image 0, -150, 60%, 60%, "parse-tree.png"


// Language
// Drawings
// Slides
// Themes


// ----------------------------------------------------------------------------
section_slide "Thank you",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Now is a good time for questions"

    picture
        translate 600, 500, 20
        rotate_z -8
        image 0, 0, 30%, 30%, "Tao3D.png"

    contents 0,
        text_box 500, -400, 600, 200,
            style "story"
            font_size 30
            anchor
                image -80, -40, 10% , 10% , "Tao3D.png"
            paragraph "This Tao3D presentation is available at"
            paragraph "https://github.com/c3d/tao3d-presentation"


disabled
    import SeasonsGreetingsTheme
    import BrightRectanglesTheme
    import PastelTrianglesTheme
    import WhiteChristmasTheme

    theme "SeasonsGreetings"
    slide "Tao3D is text based",
        subtitle "Easy to write your documents"
        § "Paragraphs for introductions"

        * "Bullets for key points"
        ** "Second-level bullets for details"
        ** "and additional information"

        * "A second bullet"
        ** "Make your points as fast as you type"
        ** "Use git to cooperate on presentations"
