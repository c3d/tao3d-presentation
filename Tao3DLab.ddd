// ----------------------------------------------------------------------------
main_title_slide "Main title",
// ----------------------------------------------------------------------------
    title "Tao3D: Like an interactive 3D LaTeX Lisp"
    subtitle
        paragraph "A presentation tool designed for developers"
        paragraph "A programming language designed for 3D animations"
    presentation_author
        paragraph "Christophe de Dinechin (dinechin@redhat.com)"

    picture
        translate 600, 500, 20
        rotate_z -8
        image 0, 0, 30%, 30%, "Tao3D.png"


// ----------------------------------------------------------------------------
base_slide "What is Tao3D?",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        text "And what is """
        italic "Interactive 3D LaTeX Lisp"
        text """ supposed to mean?"
    illustration 15%, "Tao3D.png"

    left_column
        stepping
        * "Tao3D is a programming language for real-time interactive 3D"
        ** "A programming language"
        ** "Real-time"
        ** "Interactive"
        ** "in three dimensions"
        §
        * "Like LaTeX, Tao3D is..."
        ** "Designed to create documents"
        ** "User configurable and stylable"
        §
        * "Like Lisp, Tao3D is..."
        ** "Functional"
        ** "Extensible"
        ** "Homoiconic (programs are data)"

    if smooth_step in 7.5..8.5 then
        code_position -75, -30, 700, 800
        redhat_theme "SeasonsGreetings"
    else
        code_move_default
        redhat_theme "RedHat"

    right_picture
        example_box 2, "Programming language", "example-1.ddd",
            import "example-1.ddd"
        example_box 3, "Real-time", "example-2.ddd",
            import "example-2.ddd"
        example_box 4, "Interactive", "example-3.ddd",
            import "example-3.ddd"
        example_box 5, "Three dimensions", "example-4.ddd",
            light 0
            light_position 100, 100, 1000
            show 1.5
            translate_z 150
            import "example-4.ddd"

        example_box 7, "Documents", "latex-1.ddd",
            image 0, -250, 30%, 30%, "Text Based.png"
        example_box 8, "Styles", "latex-1.ddd", false

        example_box 10, "Functional", "lisp-1.ddd",
            import "lisp-1.ddd"
        example_box 11, "Extensible", "lisp-2.ddd",
            import "lisp-2.ddd"
        example_box 12, "Homoiconic", "lisp-3.ddd",
            color "darkblue"
            translate -200, -200, 0
            font "Impact", 40
            import "lisp-3.ddd"



// ============================================================================
//
section_slide "Installation",
//
// ============================================================================
    title page_label
    subtitle "Installing or building yourself..."
    picture
        translate 600, 400, 50
        rotate_z -9
        image 0, 0, 70%, 70%, "Tao3DInstall.png"


// ----------------------------------------------------------------------------
base_slide "Installing Tao3D the easy way",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        paragraph "Download from the web site and run the installer"
        paragraph { code "https://tao3d.sourceforge.net" }
    illustration 30%, "Tao3DInstall.png"
    picture
        frame_texture 1500, 800,
            translate_y (650 * smooth_step - 1350) * fade_at(smooth_step, 1)
            scale 30% + 42% * fade_at(smooth_step, 1)
            image 0, 0, 100%, 100%, "Tao3DWebSite.png"
        rectangle 0, 100, 1500, 800
        at_step 3


// ----------------------------------------------------------------------------
base_slide "Building Tao3D from git",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        paragraph "Hard dependencies on Qt5 and LLVM"
        paragraph { code "https://github.com/c3d/tao-3D" }
    illustration 40%, "git icon.png"
    picture
        at_step 1
        code_box -400, 100, 700, 500, "Dependencies",
            text load_text "dependencies.txt"
        at_step 2
        code_box  400, 100, 700, 500, "Installation",
            text load_text "installation.txt"


// ----------------------------------------------------------------------------
base_slide "The most useful feature of Tao3D",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "The one menu entry you should use right after installation"
    illustration 30%, "Tao3D Help Icon.png"
    picture
        only_at 1,
            image 0, 0, 80%, 80%, "Tao3D Help Menu.png"
        only_at 2,
            image 0, 80, 50%, 50%, "Tao3D Help.png"
        at_step 2


// ============================================================================
//
section_slide "The Tao3D Programming Language",
//
// ============================================================================
    title page_label
    subtitle "What is different and unusual about it?"
    picture
        translate 500, 400, 50
        rotate_z -5
        locally
            radial_gradient 64, 64, 64, 128, 128,
                gradient_color   0%, "white", 90%
                gradient_color  70%, "white", 30%
                gradient_color 100%, "white",  0%
            ellipse 0, 0, 480, 220
        image 0, 0, 70%, 70%, "programming-languages.png"
        color "#F60", 70%
        rotate_z 25
        rectangle 450, 30


// ----------------------------------------------------------------------------
base_slide "Tao3D is an application of XL",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "An extensible language? What about dynamic documents?"
    illustration 20%, "XLLogo.png"
    story
        * "XL stands for ""Extensible Language"""
        * "It makes it easy to create Domain Specific Languages (DSLs)"
        * "Tao3D began with the question: """; italic "What about documents?"; text """"
        * "Tao3D is (by far) the largest DSL to date"
    picture
        at_step 3
        code_box 0, -200, 1300, 300, "A simple slide",
            colorize load_text "demos/simple-slide.ddd"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #1: My first slide", 40%, "First Slide.png"
// ----------------------------------------------------------------------------


// ----------------------------------------------------------------------------
base_slide "Representing the parse tree",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Any program can be represented with only 8 node types"
    illustration 20%, "Christmas Tree.png"
    left_column
        § "A very simple parse-tree (8 node types)"
        §
        § "Terminal nodes in the parse tree:"
        * "Integer numbers: "; code "42"
        * "Real numbers: "; code "3.1415926"
        * "Text: "; code """Hello"""; text ", "; code "'X'"
        * "Symbols: "; code "X15"; text ", "; code "foo_bar"
        text ", "; code "++"
        §
        § "Inner nodes in the parse tree:"
        * "Prefix: ";  code "sin 42"; text ", "; code "+42"
        * "Postfix: "; code "42km"; text ", "; code "42%"
        * "Infix: "; code "42 and 1"; text ", "; code "42 + 1"
        * "Block: "; code "(42)"; text ", "; code "[42]"
        text ", "; code "{42}"; text ", indentation"

    right_picture
        example_box 1..16, "Parse tree", "parse-tree.ddd",
            image 0, -120, 55%, 55%, "parse-tree.png"


// ----------------------------------------------------------------------------
base_slide "A few things to know",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Some peculiar aspects of how Tao3D reads source code"
    illustration 15%, "Puzzling.png"
    story
        § "Terminal nodes:"
        * "Number separators: "; code "1_000_000"
        text ", "; code "3.141_592_653"
        * "Based numbers (integer and real, base 2-36): "; code "2#1001_1001"
        text ", "; code "16#F.0#e-3"
        §
        § "Inner nodes:"
        * "Dynamic precedence for operators"
        * "No parentheses unless necessary: "; code "sin x"
        * "All blocks are equivalent except for precedence"
        * "Line sequences are represented by an infix ""End of line"""
        * "Indentation is represented by an ""indent"" block"
        §
        § "Some special rules"
        * "Expression vs. statement: "; code "write sin X, Y"
        * "Spacing around operators: "; code "write -A * (B-C)"
        * "Pattern matching is the basis for overloading"
    picture
        code_box_at_step 13, 470, -120, 600, 200, "Patterm matching",
            colorize load_text "patterns.ddd"


// ----------------------------------------------------------------------------
base_slide "The tree rewrite operator",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "One operator to rule them all"
    illustration 15%, "One Ring To Rule Them All.png"
    left_column
        * "The "; code "->"; text " operator (someday: "; code "is"; text ")"
        * "Reads as "; italic "transforms into"
        §
        * "Variables"
        * "Functions"
        * "Operators"
        * "Entire expressions"
        * "Pattern matching"
        * "Arbitrary notations"

    right_picture
        example_box 3, "Variable declaration", "rewrite-1.ddd", false
        example_box 4, "Function declaration", "rewrite-2.ddd", false
        example_box 5, "Operator declaration", "rewrite-3.ddd", false
        example_box 6, "Expression declaration", "rewrite-4.ddd", false
        example_box 7, "Pattern matching", "rewrite-5.ddd", false
        example_box 8, "Arbitrary notations", "rewrite-6.ddd", false


// ----------------------------------------------------------------------------
base_slide "Dynamic compilation and updates",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D is designed to facilitate dynamic workflows"
    illustration 15%, "Race Car.png"
    story
        § "Source code is translated dynamically to machine code using LLVM"
        §
        § "Tao3D will automatically reload:"
        * "Source code"
        * "Images"
        * "Shaders"
        * "Loaded text from files"
        §
        § "Use any editor you prefer for any of this"
        §
        §
        § "We will use this for our live coding sessions"
    picture
        color "white", fade_between(smooth_step, 1, 6)
        image 600, 300, 100%, 100%, "LLVM Logo.png"
        at_step 7
        anchor
            color "white"
            image 300,  100, 50%, 50%, "Emacs Icon.png"
            image 300, -150, 20%, 20%, "DxO Optics Pro.png"
            image 500,  -50, 80%, 80%, "GIMP Icon.png"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #2: Pattern Matching", 40%, "Pattern Match.png",
// ----------------------------------------------------------------------------
    translate -200, -200, 0
    scale 70%
    color "navy", 60%
    import "demos/pattern-matching.ddd"



// ============================================================================
//
section_slide "Drawing stuff",
//
// ============================================================================
    title page_label
    subtitle "Fundamental drawing model for Tao3D"
    picture
        translate 500, 400, 50
        rotate_z -5
        image 0, 0, 50%, 50%, "3D Shark.jpg"


// ----------------------------------------------------------------------------
base_slide "Drawing coordinates",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D uses a screen-centered 3D coordinates system"
    illustration 25%, "Coordinates.png"
    picture
        translate 150 * fade_between(smooth_step, 3, 4), 60, 0
        rotate_z 20 * fade_between(smooth_step, 4, 4)
        rotate_x 20 * fade_between(smooth_step, 5, 9)
        rotate_y (40 + 20 * sin time) * fade_between(smooth_step, 5, 9)


        line_color "#777"
        line_width 2
        line_arrow -600,    0, none, 600,   0, arrowhead
        line_arrow    0, -400, none,   0, 400, arrowhead
        locally
            rotate_x 90
            line_arrow    0, -400, none,   0, 400, arrowhead
        line_width 8
        line_arrow  590,    0, none, 600,   0, arrowhead
        line_arrow    0,  390, none,   0, 400, arrowhead
        locally
            rotate_x 90
            line_arrow    0, 390, none,   0, 400, arrowhead

        line_width 0
        font "Arial", 25
        color "black"
        align 0%
        vertical_align 100%
        text_box   65,   30, 120, 60, { text "(0, 0)" }
        text_box  555,   30, 120, 60, { text "(600, 0)" }
        text_box -525,   30, 120, 60, { text "(-600, 0)" }
        text_box   65, -350, 120, 60, { text "(-0, 400)" }
        text_box   65,  380, 120, 60, { text "(0, 400)" }

        at_step 1
        translate_z 50 * fade_at(smooth_step, 6)
        locally
            if smooth_step >= 7.5 then
                light 0
                light_position 300, 500, 1000
            extrude_depth 150 * fade_at(smooth_step, 7)
            color "orange"
            line_color "darkorange"
            rectangle -250, -100, 500, 200
            extrude_radius 10 * fade_at(smooth_step, 9)
            line_color "darkred"
            color "red"
            ellipse 200, 100, 500, 100
        style "code"
        color "white"
        align 50%
        vertical_align 50%
        text_box 200, 100, 500, 100,
            text "ellipse 200, 100, 500, 100"
        text_box -250, -100, 500, 200,
            text "rectangle -250, -100, 500, 200"


    picture
        only_at 1,
            text_box 300, -250, 400, 300,
                style "comment"
                paragraph "Shape coordinates are"
                paragraph { code "X, Y, Width, Height" }
                paragraph "(X,Y) is at shape center"
        only_at 2,
            text_box 300, 250, 400, 300,
                style "comment"
                text "Initially, coordinates are in pixel units and centered on the screen"
        only_at 3..4,
            text_box -100, 100, 400, 300,
                style "comment"
                paragraph "Translation and rotation change the coordinate system"
            text_box 450, -100, 400, 300,
                style "code"
                paragraph "translate 150, 0, 0"
                at_step 4
                paragraph "rotate_z 20"
        only_at 5,
            text_box -300, 200, 500, 300,
                style "comment"
                paragraph "The coordinate system is actually always in 3D with Z ""poking out"" of the screen"
            text_box 250, -100, 400, 300,
                style "code"
                paragraph "rotate_x 20"
                paragraph "rotate_y 40 + 20 * sin time"
        only_at 6,
            text_box -300, 200, 500, 300,
                style "comment"
                paragraph "3D transforms are possible even for 2D shapes"
            text_box 250, -100, 400, 300,
                style "code"
                paragraph "translate_z 50"
        only_at 7..9,
            text_box -300, 200, 400, 300,
                style "comment"
                paragraph "All 2D shapes can be extruded into 3D"
                at_step 8
                paragraph "and lighted"
            text_box 350, -100, 600, 300,
                style "code"
                if smooth_step >= 7.5 then
                    paragraph "light 0"
                    paragraph "light_position 300, 1000, 1000"
                paragraph "extrude_depth 150"
                at_step 9
                paragraph "extrude_radius 10"
        at_step 9


// ----------------------------------------------------------------------------
base_slide "Shapes",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D offers a wealth of 2D and 3D shapes"
    illustration 35%, "Basic shapes.png"
    picture
        rotate_x 0.1 * mouse_y
        rotate_y -0.1 * mouse_x

        show_shape 0, "square", {square 0, 0, 100}
        show_shape 1, "circle", {circle 0, 0, 50}
        show_shape 2, "rectangle", {rectangle 0, 0, 200, 100}
        show_shape 3, "ellipse", {ellipse 0, 0, 200, 100}
        show_shape 4, "rounded_rectangle", {rounded_rectangle 0, 0, 200, 100, 40 + 30 * sin time}
        show_shape 5, "polygon", {polygon 0, 0, 200, 100, seconds mod 6 + 3}
        show_shape 6, "star_polygon", {star_polygon 0, 0, 150, 150, seconds mod 8 + 5, 5}
        show_3d_shape 7, "sphere", {sphere 0, 0, 0, 150}
        show_3d_shape 8, "cube", {cube 0, 0, 0, 200, 100, 80}
        show_3d_shape 9, "torus", {torus 0, 0, 0, 200, 50% + 30% * sin time}
        show_3d_shape 10, "cone", {cone 0, 0, 0, 100, 50, 300; cone 0, 0, 0, 100, -50, 300}
        show_3d_shape 11, "truncated_cone", {truncated_cone 0, 0, 0, 100, 50, 300, 50% + 30% * sin time; truncated_cone 0, 0, 0, 100, -50, 300, 50% + 30% * sin time}
        show_shape 12, "text",
            text_box 0, 0, 200, 100,
                font "Brush Script MT", 80
                align 50%
                text "Hello"
        show_shape 13, "path",
            path
                move_to 0, 0
                line_to 50, 0
                line_to 50, 50
                quad_to -200, 50, -50, -50
                quad_to -50, 100, 0, -50
        show_shape 14, "arrow",
            arrow        0,  30, 200, 50, 45, 30%
            double_arrow 0, -30, 200, 50, 45, 30%
        show_shape 15, "line",
            line_width 10 * sin(0.113 * time)^2
            line_color_hsv 30 * time, 0.9, 0.8
            line_arrow -100,  60, hollow_circle, 100,  60, hollow_square
            line_arrow -100,  30, none,          100,  30, none
            line_arrow -100,   0, square,        100,   0, arrowhead
            line_arrow -100, -30, diamond,       100, -30, rounded
            line_arrow -100, -60, circle,        100, -60, square


// ----------------------------------------------------------------------------
base_slide "Paths",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "A way to draw arbitrary shapes"
    illustration 60%, "Path.png"
    left_column
        * "Built from lines, quadrics, cubics"
        * "Arbitrary paths using code"
        * "Combine basic shapes..."
        * "... including text"

    right_picture
        example_box 1, "Path components", "path-1.ddd",
            import "path-1.ddd"
        example_box 2, "Arbitrary paths", "path-2.ddd",
            translate_y -250
            import "path-2.ddd"
        example_box 3, "Shapes in paths", "path-3.ddd",
            translate_y -250
            import "path-3.ddd"
        example_box 4, "Text in path", "path-4.ddd",
            import "path-4.ddd"


// ----------------------------------------------------------------------------
base_slide "Colors",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Several color models are available for convenience"
    illustration 15%, "Colors.png"
    picture
        attribute "Name", "color ""red""", { color "red" }
        attribute "RGB", "color 30%, 50%, sin time", { color 30%, 50%, sin time }
        attribute "HSV", "color_hsv 20*time, 50%, 100%", { color_hsv 20*time, 50%, 100% }
        attribute "Web hex", "color ""#30F0CA""", { color "#30F0CA" }
        attribute "Web RGB", "color ""rgb(200,30,10)""", { color "rgb(200,30,10)" }
        attribute "Web HSV", "color ""hsv(200,30,200)""", { color "hsv(200,30,200)" }
        attribute "Alpha", "color ""red"", sin time ^ 2", { color "red", sin time  ^ 2 }


// ----------------------------------------------------------------------------
base_slide "Shape attributes",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Attributes modify the drawing of shapes and are cumulative"
    illustration 40%, "Attributes.png"
    picture
        attribute "Line color", "line_color ""blue""",
            color "red"
            line_color "blue"
            line_width 2
        attribute "Line width", "line_width 10",
            color "red"
            line_color "blue"
            line_width 10
        attribute "Texture", "texture ""Attributes.png""",
            color_hsv 20 * time, 30%, 100%
            texture "Attributes.png"
        attribute "Transforms", "texture_transform { ... }",
            color "white"
            texture "Attributes.png"
            texture_wrap true, true
            texture_transform
                scale 0.3 + 3 * sin time
                rotate_z 25 * time
                scale_x 2 / 0.8
                translate -0.5, -0.5, 0
        attribute "Extrusion", "extrude_depth 80",
            color "red"
            line_color "blue"
            translate_z 50
            rotate_y 20 * time
            extrude_depth 80

        attribute "Gradients", "radial_gradient ...",
            color "white"
            radial_gradient 50, 50, 70, 200, 80,
                gradient_color   0%, "red",   100%
                gradient_color  50%, "white",  60%
                gradient_color 100%, "blue",   80%

        attribute "Blending", "blend_function ""one"", ""one""",
            color "red"
            circle 0, 0, 40

            color "white"
            texture_unit 0
            texture "Colors.png"
            texture_wrap true, true
            texture_transform
                scale 0.3 + 3 * sin time
                rotate_z -2 * time
                scale_x 2 / 0.8
                translate -0.5, -0.5, 0
            if seconds mod 2 = 0 then
                blend_function "src_alpha", "one_minus_src_alpha"
            else
                blend_function "one", "one"


// ----------------------------------------------------------------------------
base_slide "Using locally and shape",
// ----------------------------------------------------------------------------
    title
        text "Using "; code "locally"; text " and "; code "shape"
    subtitle "Making local changes to attributes and transforms"
    illustration 40%, "Locality.png"
    left_column
        * "Attributes and geometric transforms are cumulative"
        * "Changes can be kept local to a code block with "; code "locally"
        * "A shape can be made editable by putting inside a "; code "shape"
        * "The mechanisms governing that behavior are relatively subtle"
        §
        * "Recommended: edit only small blank documents, then copy-paste and replace "; code "shape"; text " with "; code "locally"

    right_picture
        example_box 1, "Cumulative attributes", "locally-1.ddd",
            import "locally-1.ddd"
        example_box 2, "Local changes", "locally-2.ddd",
            import "locally-2.ddd"
        example_box 3, "Editable shape", "locally-3.ddd",
            import "locally-3.ddd"
        example_box 4, "Multiple editable shapes", "locally-4.ddd",
            import "locally-4.ddd"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #3: Building a clock", 25%, "Clock.png",
// ----------------------------------------------------------------------------
    picture
        translate_y -200
        scale 80%
        import "demos/clock.ddd"


// ----------------------------------------------------------------------------
base_slide "3D objects",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Drawing complete 3D models"
    illustration 50%, "3D Shapes.png"
    picture
        translate_y 120

        light 0
        light_position 1000, 1000, 1000
        light_specular "white"

        rotate_x 0.1 * mouse_y
        rotate_y -0.1 * mouse_x + 180
        rotate_x -105
        object 0, 0, 0, 800, 800, 800, "models/Resurs3D_Audi_S5.obj"

    picture
        code_box 0, -200, 1300, 500, "Loading a 3D object",
            colorize load_text "demos/loading-object.ddd"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #4: 3D objects and lighting", 60%, "3D Shapes.png"
// ----------------------------------------------------------------------------



// ============================================================================
//
section_slide "Images, movies and textures",
//
// ============================================================================
    title page_label
    subtitle "So many ways to make things perty"
    picture
        translate 500, 400, 50
        rotate_z -5
        image 0, 0, 50%, 50%, "3D-TV.png"


// ----------------------------------------------------------------------------
base_slide "Images, pictures and drawings",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Loading images from files or URLs"
    illustration 25%, "3D Drawing.png"

    left_column
        * "All common image formats"
        * "Support for transparency"
        * "Load images from URLs"
        * "Images can be used as textures"
        * "Colorization"
        * "Texture dimensions"
        * "And more bizarre things"

    right_picture
        example_box 1, "Image from file", "picture-1.ddd",
            import "picture-1.ddd"
        example_box 2, "Transparent image", "picture-2.ddd",
            import "picture-2.ddd"
        example_box 3, "Image from URL", "picture-3.ddd",
            import "picture-3.ddd"
        example_box 4, "Image textures", "picture-4.ddd",
            import "picture-4.ddd"
        example_box 5, "Colorization", "picture-5.ddd",
            import "picture-5.ddd"
        example_box 6, "Texture dimensions", "picture-6.ddd",
            import "picture-6.ddd"
        example_box 7, "Image packing", "picture-7.ddd",
            import "picture-7.ddd"


// ----------------------------------------------------------------------------
base_slide "Movie playback",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Using the VLC library, Tao3D plays back any movie"
    picture { image 660, 550, 30%, 30%, "film-reel.png" }

    right_column
        * "Use "; code "movie"; text " for playback"
        * "Requires module: "; code "VLCAudioVideo"
        * "It provides "; code "movie_texture"
        * "... as well as "; code "movie_time"
        * "... and "; code "movie_set_volume"
        * "Pass VLC options after "; code "##"

    left_picture
        example_box 1, "Movie playback", "vlc-1.ddd",
            translate 400, -100, 0
            import "vlc-1.ddd"
        example_box 2, "VLC Module", "vlc-2.ddd", false
        example_box 3, "Movie texture", "vlc-3.ddd",
            translate 400, -150, 0
            scale 60%
            import "vlc-3.ddd"
            if smooth_step in 2.8..2.9 then
                movie_set_time ASTEROIDS, 0.0
        example_box 4, "Movie time", "vlc-4.ddd",
            translate 400, -200, 0
            scale 50%
            import "vlc-4.ddd"
        example_box 5, "Movie volume", "vlc-5.ddd",
            translate 400, -200, 0
            import "vlc-5.ddd"
            if smooth_step >= 5.9 then
                movie_drop HOLY_GRAIL
        example_box 6, "VLC options", "vlc-6.ddd",
            translate 0, -180, 0
            import "vlc-6.ddd"



// ----------------------------------------------------------------------------
base_slide "Integrating movies in a narrative",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        paragraph "Tao3D can perform actions based on movie time"
        paragraph "(Based on a presentation of c3d/Build)"
    picture { image 660, 550, 30%, 30%, "film-reel.png" }

    ASK_ME_THE_QUESTION -> "movies/Monty Python.mp4##start-time=81"
    picture
        text_box -550, 0, 400, 800,
            style "story"
            paragraph "Autoconf looks a bit like this scene of Monty Python" fr "On se dirait un peu dans Monty Python"
            skip_directly_to 5
            current_step := 0
            show fade_at(movie_time ASK_ME_THE_QUESTION, 98)
            * "If you run Linux, it just works" fr "Sous Linux, tout va bien (en général)"
            show fade_at(movie_time ASK_ME_THE_QUESTION, 108)
            * "For others, it looks deceptively easy" fr "Pour les autres, ça a l'air facile"
            show fade_at(movie_time ASK_ME_THE_QUESTION, 132)
            * "But they need to answer really hard questions" fr "Mais ils ont à répondre à des questions difficiles"

            show fade_at(movie_time ASK_ME_THE_QUESTION, 158)
            * "Or are forced into unwanted decisions" fr "Ou sont forcés à faire certains choix"
            show fade_at(movie_time ASK_ME_THE_QUESTION, 182)
            * "While true bosses know more than autoconf ever will" fr "Quant aux vrais durs, ils savent mieux que le système"

    picture
        movie 200, 0, 170%, 170%, ASK_ME_THE_QUESTION
        movie_set_volume ASK_ME_THE_QUESTION, 100%
        on "pageexit",
            movie_drop ASK_ME_THE_QUESTION

        subtitle_text -> ""
        subtitle Time, Text ->
            if movie_time ASK_ME_THE_QUESTION >= Time then
                subtitle_text := Text

        subtitle 80, "Let me use configure, I'm not afraid" fr "J'ai même pas peur d'utiliser configure"
        subtitle 84, "What is uname?" fr "Que dit uname?"
        subtitle 86.5, "Linux 4.12.9-300.fc26.x86_64 #1 SMP x86_64 x86_64 x86_64 GNU/Linux"
        subtitle 89, "What is your compiler?" fr "Quel est votre compilateur?"
        subtitle 91, "gcc (GCC) 7.2.1 20170915 (Red Hat 7.2.1-2)"
        subtitle 94, "Does your GCC accept the -fPIC option?" fr "Est-ce que votre GCC accepte l'option -fPIC?"
        subtitle 96, "Yes" fr "Oui"
        subtitle 97, "Perfect. Now type 'make'" fr "Parfait. Tapez 'make' maintenant"
        subtitle 100, "Oh! Thank you. Thank you very much" fr "Ah, merci. Merci beaucoup"
        subtitle 103, "(program is compiling)" fr "(le programme compile)"

        subtitle 106.5, "That's easy!" fr "Mais c'est super facile!"
        subtitle 111, "user@machine ~ > _"
        subtitle 113, "This program uses autotools" fr "Ce programme utilise autotools"
        subtitle 115, "Run ./configure to automatically select the correct options" fr "Utilisez ./configure pour sélectionner les bonnes options"
        subtitle 117, "And then run 'make'" fr "Et ensuite vous pourrez utiliser 'make'"

        subtitle 121, "Let me use configure, I'm not afraid" fr "J'ai même pas peur d'utiliser configure"
        subtitle 123,  "What is uname?" fr "Que dit uname?"
        subtitle 125, "Darwin Darwin Kernel Version 17.2.0:; root:xnu-4570.20.62~3/RELEASE_X86_64 x86_64"
        subtitle 127, "What is your compiler?" fr "Quel est votre compilateur?"
        subtitle 129, "Apple LLVM version 9.0.0 (clang-900.0.37)"
        subtitle 131, "What is the option to force a read-only GOT?" fr "Quelle est l'option pour forcer la TOG à être en lecture seule?"
        subtitle 134, "..."
        subtitle 136, "I don't know that!" fr "Mais j'en sais rien!"
        subtitle 137, "configure: error: package 'autotools' failed"

        subtitle 146, "[user@machine]% █"
        subtitle 148, "What is uname?" fr "Que dit uname?"
        subtitle 150, "Linux 3.13.0-24-generic #46-Ubuntu SMP GNU/Linux"
        subtitle 152, "What is your compiler?" fr "Quel est votre compilateur?"
        subtitle 154.2, "gcc (Ubuntu 4.8.2-19ubuntu1) 4.8.2"
        subtitle 156, "Does your GCC accept the -fPIC option?" fr "Est-ce que votre GCC accepte l'option -fPIC?"
        subtitle 159, "Yes. Ah, wait, I'm doing a static lib" fr "Oui. Ah, en fait je fais une librairies statique"
        subtitle 161, "configure: error: don't know how to get rid of -fPIC"
        subtitle 167, "Use of uninitialized value $msg in concatenation (.) or string at /usr/local/Cellar/autoconf/2.69/bin/autom4te line 1026."

        subtitle 168.5, "king@world[master] spice> ▄"
        subtitle 170, "What is uname?" fr "Que dit uname?"
        subtitle 172, "Darwin Darwin Kernel Version 17.2.0:; root:xnu-4570.20.62~3/RELEASE_X86_64 x86_64"
        subtitle 174, "What is your compiler?" fr "Quel est votre compilateur?"
        subtitle 176.2, "Apple LLVM version 9.0.0 (clang-900.0.37)"
        subtitle 179, "Does your program require the -ObjC option to compile sources?" fr "Est-ce que votre programme a besoin de l'option -ObjC pour compiler?"
        subtitle 183, "What do you mean, do the source include <AppKit.h> or <Foundation.h>?" fr "Ça dépend, est ce que les sources utilisent <AppKit.h> ou <Foundation.h>?"
        subtitle 186, "configure: error: can't add per-source option depending on headers"
        subtitle 188.3, "(uninstalling autoconf and automake)"
        subtitle 194.8, "How do you know so much about -ObjC?" fr "Comment savez vous toutes ces choses à propos de -ObjC?"
        subtitle 197, "Well, you have to know these things when you are a real real programmer, you know" fr "Il faut apprendre ce genre de choses quand on est un développeur, tu sais"

        on "key:Return", { movie_set_time ASK_ME_THE_QUESTION, 195 }

        box 200, -350, 1100, 100,
            font "Impact", 30
            color "yellow"
            align 50%
            vertical_align 50%
            paragraph subtitle_text


// ----------------------------------------------------------------------------
base_slide "Dynamic textures",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Generate textures using code"
    illustration 15%, "Dynamic Texture.png"

    left_column
        * "Generate texture with code"
        * "Accelerate rendering"
        * "Texturing effects"


    right_picture
        example_box 1, "Dynamic texture", "dynamic-1.ddd",
            import "dynamic-1.ddd"
        example_box 2, "Acceleration", "dynamic-2.ddd",
            import "dynamic-2.ddd"
        example_box 3, "Texturing effect", "dynamic-3.ddd",
            import "dynamic-3.ddd"



// ----------------------------------------------------------------------------
demo_slide "Live Coding #5: Dynamic textures", 30%, "Dynamic Texture.png"
// ----------------------------------------------------------------------------



// ============================================================================
//
section_slide "Text boxes and layouts",
//
// ============================================================================
    title page_label
    subtitle "Texts are a central element of way too many presentations"
    picture
        translate 500, 400, 50
        rotate_z -5
        locally
            radial_gradient 64, 64, 64, 128, 128,
                gradient_color   0%, "white", 80%
                gradient_color  80%, "white", 60%
                gradient_color 100%, "white",  0%
            ellipse 0, 0, 500, 400
        image 0, 0, 80%, 80%, "Text Layout.png"


// ----------------------------------------------------------------------------
base_slide "Text boxes and text layout",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Real-time text layout with fine-grained control"
    illustration 35%, "Text Layout.png"

    left_column
        * "Text layout happens in "; code "text_box"
        * "Fonts, attributes, paragraphs"
        * "Horizontal and vertical alignment"
        * "Alignments are proportional"
        * "Justification is also proportional"
        * "Spread and last-line justification"
        * "Right-to-left support"
        §
        § "Sill quite a few bugs..."

    right_picture
        color "black", 10% * fade_between(smooth_step, 1, 7)
        line_color "lightgray", 90% * fade_between(smooth_step, 1, 7)
        rectangle 0, -200, 600, 300

        font "Arial", 45
        color "#400"
        line_color "transparent"

        example_box 1, "Text box", "text-1.ddd",
            translate_y -200
            import "text-1.ddd"
        example_box 2, "Fonts and attributes", "text-2.ddd",
            translate_y -200
            import "text-2.ddd"
        example_box 3, "Horizontal and vertical", "text-3.ddd",
            translate_y -200
            font "Arial", bold, 25
            import "text-3.ddd"
        example_box 4, "Proportional alignment", "text-4.ddd",
            translate_y -200
            import "text-4.ddd"
        example_box 5, "Justification", "text-5.ddd",
            translate_y -200
            font "Arial", 18
            import "text-5.ddd"
        example_box 6, "Spread", "text-6.ddd",
            translate_y -200
            font "Arial", 35
            import "text-6.ddd"
        example_box 7, "Right-to-left text layout", "text-7.ddd",
            translate_y -200
            font "Arial Unicode MS", 35
            import "text-7.ddd"


// ----------------------------------------------------------------------------
base_slide "Auto-scaling",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "The difference between background and content"

    background -500,
        color "white", 25% * fade_at(smooth_step, 3) - 15% * fade_at(smooth_step, 6)
        image 0, 0, 150%, 150%, "Background.png"

    contents 200,
        color "white", 95% * fade_at(smooth_step, 4) - 75% * fade_at(smooth_step, 6)
        image 400, 0, 100%, 100%, "Tux.png"

    contents 100,
        color "white", 99%
        image 600, 500, 20%, 20%, "Tux.png"
    left_column
        * "You can control layout manually"
        * "But some things are frequent"
        ** "A background image (crop)"
        ** "Significant content (fit)"
        * "What about aspect resizing?"
        ** "Using "; code "background"; text " and "; code "contents"
        ** "These functions also deal with depth (compensate for perspective)"
    right_picture
        example_box 6..7, "Background and contents", "autoscale.ddd", false


// ----------------------------------------------------------------------------
demo_slide "Live Coding #6: Slide layouts", 40%, "pinky-decor.png"
// ----------------------------------------------------------------------------


// ----------------------------------------------------------------------------
base_slide "Reactive programming",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Responding to events by re-evaluating code"
    illustration 30%, "Events.png"

    left_column
        * "Mouse events"
        ** "Compensates for perspective"
        * "Keyboard events"
        * "Time and page time"
        §
        § "But... How does it work?"
    right_picture
        example_box 1..2, "Mouse control", "reactive-1.ddd",
            translate 200 * fade_at(smooth_step, 2), -200, 0
            rotate_x 20 * fade_at(smooth_step, 2)
            rotate_y 45 * fade_at(smooth_step, 2)
            import "reactive-1.ddd"
        example_box 3, "Keyboard control", "reactive-2.ddd",
            translate 0, -200, 0
            import "reactive-2.ddd"
        example_box 4, "Time-dependent code", "reactive-3.ddd",
            translate 0, -200, 0
            font "Arial", 30
            align_center; vertical_align_center
            import "reactive-3.ddd"



// ----------------------------------------------------------------------------
base_slide "Optimized refresh and redraw",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D re-evaluate parts of the document independantly"
    illustration 60%, "upcoming-events.png"

    left_column
        * "Events trigger a refresh"
        * "Refresh is limited by "; code "locally"
        §
        § "But..."
        * "Beware of saved variables"
        * "Only one "; code "I"; text ", has value "; code "6"; text " in refresh"
        * "Fix #1: refresh one level up"
        * "(This is a bit inefficient)"
        * "Fix #2: Capture a copy of the value"
        §
        § "All this matters for power and performance..."


    right_picture
        example_box 1..2, "Refresh", "reactive-4.ddd",
            translate_y -200

            import "reactive-4.ddd"

            text_box -800, 0, 600, 400,
                style "code"
                paragraph "key_hits=" & key_hits
                paragraph "mouse_hits=" & mouse_hits
                paragraph "time_hits=" & time_hits
                paragraph "seconds_hits=" & seconds_hits
                paragraph "global_hits=" & global_hits
                refresh 0

            if smooth_step in 0.8..0.9 then
                key_hits := 0
                mouse_hits := 0
                time_hits := 0
                seconds_hits := 0
                global_hits := 0

        example_box 4..5, "Saved variable", "reactive-5.ddd",
            translate_y -200
            import "reactive-5.ddd"

        example_box 6..7, "Force redraw", "reactive-6.ddd",
            translate_y -200
            import "reactive-6.ddd"

        example_box 8, "Variable capture", "reactive-7.ddd",
            translate_y -200
            import "reactive-7.ddd"


// ----------------------------------------------------------------------------
base_slide "Reversible animations",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Animations should be able to play back and forth"
    illustration 100%, "Back and Forth.png"

    left_column
        * "Use module "; code "Animate"
        * "Stepping position is "; code "step"
        * "Smoothed as "; code "smooth_step"
        * "Skipping steps with "; code "skip"
        * "Transitions with "; code "fade_at"
        * "Variants like "; code "fade_out_at"
        * "and "; code "fade_between"


    right_picture
        example_box 2..4, "Stepping", "reversible-1.ddd",
            translate 0, -200, 0
            import "reversible-1.ddd"
            text_box 0, -200, 600, 400,
                style "code"
                font_size 30
                paragraph "step=" & step
                show fade_at(smooth_step, 3)
                paragraph "smooth_step=" & smooth_step
                refresh 0

        example_box 5, "Fading", "reversible-2.ddd",
            translate_y -200
            import "reversible-2.ddd"

        example_box 6, "Fading out", "reversible-3.ddd",
            translate_y -200
            import "reversible-3.ddd"

        example_box 7, "Fading between", "reversible-4.ddd",
            translate_y -200
            import "reversible-4.ddd"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #7: Events and animations", 50%, "Timeline.png"
// ----------------------------------------------------------------------------



// ============================================================================
//
section_slide "Themes",
//
// ============================================================================
    title page_label
    subtitle "Themes let you easliy adjust the look and behavior of your slides"
    picture
        translate 500, 400, 50
        rotate_z -5
        image 0, 0, 80%, 80%, "Design.png"


// ----------------------------------------------------------------------------
base_slide "Using pre-existing themes",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D comes up with a few semi-useless themes"
    illustration 60%, "Graphics Design.png"

    left_column
        * "Themes are exposed as modules"
        * "Load several simultaneously"
        * "Select a theme using "; code "theme"

    right_picture
        example_box 1..3, "Themes", "theme-1.ddd", false


// ----------------------------------------------------------------------------
demo_slide "Live Coding #8: Using existing themes", 100%, "Graphics Design.png"
// ----------------------------------------------------------------------------


// ----------------------------------------------------------------------------
base_slide "The theme.xl file",
// ----------------------------------------------------------------------------
    title { text "The "; code "theme.xl"; text "file" }
    subtitle "Configuring and tweaking themes"
    illustration 70%, "Control Panel.png"

    story
        vertical_align -10%
        * "Tao3D automatically loads "; code "theme.xl"
        * "Think of it as a style sheet"
        * "It is heavily based on pattern matching"
        * "You can customize on theme, master or style"

    picture
        at_step 1
        code_box 0, -100, 1200, 600, "Theme example",
            colorize load_text "theme-example.xl"


// ============================================================================
//
section_slide "Data processing",
//
// ============================================================================
    title page_label
    subtitle "Good presentations often depend on good data"
    picture
        translate 500, 400, 50
        rotate_z -5
        image 0, 0, 60%, 60%, "big-data-analytics.png"


// ----------------------------------------------------------------------------
base_slide "Text manipulations",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Fairly standard operations on text"
    illustration 60%, "Graphics Design.png"

    left_column
        * "Text concatenation with "; code "&"
        * "Text ranges with "; code "text_range"
        * "Read text files with "; code "load_text"

    right_picture
        example_box 1, "Text concatenation", "text-manip-1.ddd",
            font "Arial", 40
            translate_y -200
            color "darkred"
            import "text-manip-1.ddd"
        example_box 2, "Text ranges", "text-manip-2.ddd",
            font "Menlo", 40
            translate_y -200
            color "darkgreen"
            import "text-manip-2.ddd"
        example_box 3, "Loading text from file", "text-manip-3.ddd",
            font "Arial", 20
            translate_y -200
            color "darkblue"
            import "text-manip-3.ddd"


// ----------------------------------------------------------------------------
base_slide "Simple lists",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Representing data as lists using pattern matching"
    illustration 60%, "Graphics Design.png"

    left_column
        * "A list? No, an infix"; code ","
        * "Operating on lists"
        * "... with the power of code"
        * "Map operation: "; code "with"
        * "... which can contain ranges"
        * "Filter operation: "; code "where"
        * "Reduce operation"
        * "(Really badly broken - No live coding on that one)"

    right_picture
        demo_adjust ->
            translate -1000, -350, 0
            font "Arial", 30
            align 50%
            vertical_align 50%
        example_box 1, "Infix operator", "list-1.ddd", false
        example_box 2, "List expansion", "list-2.ddd",
            demo_adjust
            color "darkred"
            import "list-2.ddd"
        example_box 3, "Code in list", "list-2b.ddd",
            demo_adjust
            color "darkred"
            import "list-2b.ddd"
        example_box 4, "Map", "list-3.ddd",
            demo_adjust
            color "darkgreen"
            import "list-3.ddd"
        example_box 5, "Ranges in map", "list-3b.ddd",
            demo_adjust
            color "darkgreen"
            import "list-3b.ddd"
        example_box 6, "Filter", "list-4.ddd",
            demo_adjust
            color "darkblue"
            import "list-4.ddd"
        example_box 7..8, "Reduce", "list-5.ddd",
            demo_adjust
            color "darkred"
            font "Arial", 50
            import "list-5.ddd"


// ----------------------------------------------------------------------------
base_slide "Loading data from external files",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D can be used to interactively present data"
    illustration 60%, "icon-big-data.png"

    left_column
        * "Load data with "; code "load_csv"
        * "Better watched full-screen"
        * "Irregular data"
        * "Reloaded dynamically"
        * "Works over the network"

    right_picture
        example_box 1..5, "Themes", "load_csv-1.ddd",
            color "black"
            adjust fade_between(smooth_step, 2, 2)
            adjust S:real ->
                translate -400*S, 250*S - 150, 100 * S
                scale 60% + 250% * S
            rounded_rectangle 0,0, 800, 600, 20
            rotate_y 5 * time
            import "load_csv-1.ddd"



// ----------------------------------------------------------------------------
demo_slide "Live Coding #9: Hipparcos Star Map", 100%, "icon-big-data.png"
// ----------------------------------------------------------------------------


// ----------------------------------------------------------------------------
base_slide "Regular expressions",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "How do you think the colorization works?"
    illustration 60%, "RegExRX.png"

    left_column
        * "Check matches with "; code "regexp_match"
        * "Parse text with "; code "regexp_parse"
        * "Select a theme using "; code "theme"
        §
        § "Any idea how colorization works now?"


    right_picture
        example_box 1, "Regexp match", "regexp-1.ddd",
            translate -200, -150, 0
            font "Arial", 60
            import "regexp-1.ddd"
        example_box 2, "Regexp parse", "regexp-2.ddd",
            translate -600, -150, 0
            font "Arial", 60
            import "regexp-2.ddd"
        example_box 3, "Regexp parse all", "regexp-3.ddd",
            translate -800, -150, 0
            font "Arial", 60
            import "regexp-3.ddd"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #10: Source code colorization", 100%, "Graphics Design.png"
// ----------------------------------------------------------------------------


// ----------------------------------------------------------------------------
base_slide "Extending Tao3D with C++ modules",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D comes up with a few semi-useless themes"
    illustration 60%, "Graphics Design.png"

    left_column
        * "Themes are exposed as modules"
        * "Load several simultaneously"
        * "Select a theme using "; code "theme"

    right_picture
        example_box 1..3, "Themes", "theme-1.ddd", false



// ============================================================================
//
section_slide "Shaders",
//
// ============================================================================
    title page_label
    subtitle "Yet another programming language to deal with"
    picture
        translate 500, 400, 50
        rotate_z -5
        image 0, 0, 80%, 80%, "big-data-analytics.png"


// ----------------------------------------------------------------------------
base_slide "Using shaders",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D comes up with a few semi-useless themes"
    illustration 60%, "Graphics Design.png"

    left_column
        * "Themes are exposed as modules"
        * "Load several simultaneously"
        * "Select a theme using "; code "theme"

    right_picture
        example_box 1..3, "Themes", "theme-1.ddd", false


// ----------------------------------------------------------------------------
base_slide "Image processing and filters",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D comes up with a few semi-useless themes"
    illustration 60%, "Graphics Design.png"

    left_column
        * "Themes are exposed as modules"
        * "Load several simultaneously"
        * "Select a theme using "; code "theme"

    right_picture
        example_box 1..3, "Themes", "theme-1.ddd", false


// ----------------------------------------------------------------------------
base_slide "Real-time ray-tracing",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D comes up with a few semi-useless themes"
    illustration 60%, "Graphics Design.png"

    left_column
        * "Themes are exposed as modules"
        * "Load several simultaneously"
        * "Select a theme using "; code "theme"

    right_picture
        example_box 1..3, "Themes", "theme-1.ddd", false


// ----------------------------------------------------------------------------
base_slide "Real-time ray-marching",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D comes up with a few semi-useless themes"
    illustration 60%, "Graphics Design.png"

    left_column
        * "Themes are exposed as modules"
        * "Load several simultaneously"
        * "Select a theme using "; code "theme"

    right_picture
        example_box 1..3, "Themes", "theme-1.ddd", false


// ----------------------------------------------------------------------------
demo_slide "Live Coding #11: Shader editing", 100%, "Graphics Design.png"
// ----------------------------------------------------------------------------




// ============================================================================
//
section_slide "Oh, and one more thing...",
//
// ============================================================================
    title page_label
    subtitle "Tao3D is connected. And this opens a world of possibilities"
    picture
        translate 500, 400, 50
        rotate_z -5
        image 0, 0, 80%, 80%, "steve-jobs.png"

import "Quizz.ddd"



// ----------------------------------------------------------------------------
section_slide "Thank you",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Now is a good time for questions"

    picture
        translate 600, 500, 20
        rotate_z -8
        image 0, 0, 30%, 30%, "Tao3D.png"

    contents 0,
        text_box 500, -400, 600, 200,
            style "story"
            font_size 30
            anchor
                image -80, -40, 10% , 10% , "Tao3D.png"
            paragraph "This Tao3D presentation is available at"
            paragraph "https://github.com/c3d/tao3d-presentation"


disabled
    import SeasonsGreetingsTheme
    import BrightRectanglesTheme
    import PastelTrianglesTheme
    import WhiteChristmasTheme

    theme "SeasonsGreetings"
    slide "Tao3D is text based",
        subtitle "Easy to write your documents"
        § "Paragraphs for introductions"

        * "Bullets for key points"
        ** "Second-level bullets for details"
        ** "and additional information"

        * "A second bullet"
        ** "Make your points as fast as you type"
        ** "Use git to cooperate on presentations"
