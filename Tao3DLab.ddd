// ----------------------------------------------------------------------------
main_title_slide "Main title",
// ----------------------------------------------------------------------------
    title "Tao3D: Like an interactive 3D LaTeX Lisp"
    subtitle
        paragraph "A presentation tool designed for developers"
        paragraph "A programming language designed for 3D animations"
    presentation_author
        paragraph "Christophe de Dinechin (dinechin@redhat.com)"

    picture
        translate 600, 500, 20
        rotate_z -8
        image 0, 0, 30%, 30%, "Tao3D.png"


// ----------------------------------------------------------------------------
base_slide "What is Tao3D?",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        text "And what is """
        italic "Interactive 3D LaTeX Lisp"
        text """ supposed to mean?"
    illustration 15%, "Tao3D.png"

    left_column
        stepping
        * "Tao3D is a programming language for real-time interactive 3D"
        ** "A programming language"
        ** "Real-time"
        ** "Interactive"
        ** "in three dimensions"
        §
        * "Like LaTeX, Tao3D is..."
        ** "Designed to create documents"
        ** "User configurable and stylable"
        §
        * "Like Lisp, Tao3D is..."
        ** "Functional"
        ** "Extensible"
        ** "Homoiconic (programs are data)"

    if smooth_step in 7.5..8.5 then
        code_position -75, -30, 700, 800
        redhat_theme "SeasonsGreetings"
    else
        code_move_default
        redhat_theme "RedHat"

    right_picture
        example_box 2, "Programming language", "example-1.ddd",
            import "example-1.ddd"
        example_box 3, "Real-time", "example-2.ddd",
            import "example-2.ddd"
        example_box 4, "Interactive", "example-3.ddd",
            import "example-3.ddd"
        example_box 5, "Three dimensions", "example-4.ddd",
            light 0
            light_position 100, 100, 1000
            show 1.5
            translate_z 150
            import "example-4.ddd"

        example_box 7, "Documents", "latex-1.ddd",
            image 0, -250, 30%, 30%, "Text Based.png"
        example_box 8, "Styles", "latex-1.ddd", false

        example_box 10, "Functional", "lisp-1.ddd",
            import "lisp-1.ddd"
        example_box 11, "Extensible", "lisp-2.ddd",
            import "lisp-2.ddd"
        example_box 12, "Homoiconic", "lisp-3.ddd",
            color "darkblue"
            translate -200, -200, 0
            font "Impact", 40
            import "lisp-3.ddd"



// ============================================================================
//
section_slide "Installation",
//
// ============================================================================
    title page_label
    subtitle "Installing or building yourself..."
    picture
        translate 600, 400, 50
        rotate_z -9
        image 0, 0, 70%, 70%, "Tao3DInstall.png"


// ----------------------------------------------------------------------------
base_slide "Installing Tao3D the easy way",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        paragraph "Download from the web site and run the installer"
        paragraph { code "https://tao3d.sourceforge.net" }
    illustration 30%, "Tao3DInstall.png"
    picture
        frame_texture 1500, 800,
            translate_y (650 * smooth_step - 1350) * fade_at(smooth_step, 1)
            scale 30% + 42% * fade_at(smooth_step, 1)
            image 0, 0, 100%, 100%, "Tao3DWebSite.png"
        rectangle 0, 100, 1500, 800
        at_step 3


// ----------------------------------------------------------------------------
base_slide "Building Tao3D from git",
// ----------------------------------------------------------------------------
    title page_label
    subtitle
        paragraph "Hard dependencies on Qt5 and LLVM"
        paragraph { code "https://github.com/c3d/tao-3D" }
    illustration 40%, "git icon.png"
    picture
        at_step 1
        code_box -400, 100, 700, 500, "Dependencies",
            text load_text "dependencies.txt"
        at_step 2
        code_box  400, 100, 700, 500, "Installation",
            text load_text "installation.txt"


// ----------------------------------------------------------------------------
base_slide "The most useful feature of Tao3D",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "The one menu entry you should use right after installation"
    illustration 30%, "Tao3D Help Icon.png"
    picture
        only_at 1,
            image 0, 0, 80%, 80%, "Tao3D Help Menu.png"
        only_at 2,
            image 0, 80, 50%, 50%, "Tao3D Help.png"
        at_step 2


// ============================================================================
//
section_slide "The Tao3D Programming Language",
//
// ============================================================================
    title page_label
    subtitle "What is different and unusual about it?"
    picture
        translate 500, 400, 50
        rotate_z -5
        locally
            radial_gradient 64, 64, 64, 128, 128,
                gradient_color   0%, "white", 90%
                gradient_color  70%, "white", 30%
                gradient_color 100%, "white",  0%
            ellipse 0, 0, 480, 220
        image 0, 0, 70%, 70%, "programming-languages.png"
        color "#F60", 70%
        rotate_z 25
        rectangle 450, 30


// ----------------------------------------------------------------------------
base_slide "Tao3D is an application of XL",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "An extensible language? What about dynamic documents?"
    illustration 20%, "XLLogo.png"
    story
        * "XL stands for ""Extensible Language"""
        * "It makes it easy to create Domain Specific Languages (DSLs)"
        * "Tao3D began with the question: """; italic "What about documents?"; text """"
        * "Tao3D is (by far) the largest DSL to date"
    picture
        at_step 3
        code_box 0, -200, 1300, 300, "A simple slide",
            colorize load_text "demos/simple-slide.ddd"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #1: My first slide", 40%, "First Slide.png"
// ----------------------------------------------------------------------------


// ----------------------------------------------------------------------------
base_slide "Representing the parse tree",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Any program can be represented with only 8 node types"
    illustration 20%, "Christmas Tree.png"
    left_column
        § "A very simple parse-tree (8 node types)"
        §
        § "Terminal nodes in the parse tree:"
        * "Integer numbers: "; code "42"
        * "Real numbers: "; code "3.1415926"
        * "Text: "; code """Hello"""; text ", "; code "'X'"
        * "Symbols: "; code "X15"; text ", "; code "foo_bar"
        text ", "; code "++"
        §
        § "Inner nodes in the parse tree:"
        * "Prefix: ";  code "sin 42"; text ", "; code "+42"
        * "Postfix: "; code "42km"; text ", "; code "42%"
        * "Infix: "; code "42 and 1"; text ", "; code "42 + 1"
        * "Block: "; code "(42)"; text ", "; code "[42]"
        text ", "; code "{42}"; text ", indentation"

    right_picture
        example_box 1..16, "Parse tree", "parse-tree.ddd",
            image 0, -120, 55%, 55%, "parse-tree.png"


// ----------------------------------------------------------------------------
base_slide "A few things to know",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Some peculiar aspects of how Tao3D reads source code"
    illustration 15%, "Puzzling.png"
    story
        § "Terminal nodes:"
        * "Number separators: "; code "1_000_000"
        text ", "; code "3.141_592_653"
        * "Based numbers (integer and real, base 2-36): "; code "2#1001_1001"
        text ", "; code "16#F.0#e-3"
        §
        § "Inner nodes:"
        * "Dynamic precedence for operators"
        * "No parentheses unless necessary: "; code "sin x"
        * "All blocks are equivalent except for precedence"
        * "Line sequences are represented by an infix ""End of line"""
        * "Indentation is represented by an ""indent"" block"
        §
        § "Some special rules"
        * "Expression vs. statement: "; code "write sin X, Y"
        * "Spacing around operators: "; code "write -A * (B-C)"
        * "Pattern matching is the basis for overloading"
    picture
        code_box_at_step 13, 470, -120, 600, 200, "Patterm matching",
            colorize load_text "patterns.ddd"


// ----------------------------------------------------------------------------
base_slide "The tree rewrite operator",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "One operator to rule them all"
    illustration 15%, "One Ring To Rule Them All.png"
    left_column
        * "The "; code "->"; text " operator (someday: "; code "is"; text ")"
        * "Reads as "; italic "transforms into"
        §
        * "Variables"
        * "Functions"
        * "Operators"
        * "Entire expressions"
        * "Pattern matching"
        * "Arbitrary notations"

    right_picture
        example_box 3, "Variable declaration", "rewrite-1.ddd", false
        example_box 4, "Function declaration", "rewrite-2.ddd", false
        example_box 5, "Operator declaration", "rewrite-3.ddd", false
        example_box 6, "Expression declaration", "rewrite-4.ddd", false
        example_box 7, "Pattern matching", "rewrite-5.ddd", false
        example_box 8, "Arbitrary notations", "rewrite-6.ddd", false


// ----------------------------------------------------------------------------
base_slide "Dynamic compilation and updates",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D is designed to facilitate dynamic workflows"
    illustration 15%, "Race Car.png"
    story
        § "Source code is translated dynamically to machine code using LLVM"
        §
        § "Tao3D will automatically reload:"
        * "Source code"
        * "Images"
        * "Shaders"
        * "Loaded text from files"
        §
        § "Use any editor you prefer for any of this"
        §
        §
        § "We will use this for our live coding sessions"
    picture
        color "white", fade_between(smooth_step, 1, 6)
        image 600, 300, 100%, 100%, "LLVM Logo.png"
        at_step 7
        anchor
            color "white"
            image 300,  100, 50%, 50%, "Emacs Icon.png"
            image 300, -150, 20%, 20%, "DxO Optics Pro.png"
            image 500,  -50, 80%, 80%, "GIMP Icon.png"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #2: Pattern Matching", 40%, "Pattern Match.png",
// ----------------------------------------------------------------------------
    translate -200, -200, 0
    scale 70%
    color "navy", 60%
    import "demos/pattern-matching.ddd"



// ============================================================================
//
section_slide "Drawing stuff",
//
// ============================================================================
    title page_label
    subtitle "Fundamental drawing model for Tao3D"
    picture
        translate 500, 400, 50
        rotate_z -5
        image 0, 0, 50%, 50%, "3D Shark.jpg"


// ----------------------------------------------------------------------------
base_slide "Drawing coordinates",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D uses a screen-centered 3D coordinates system"
    illustration 25%, "Coordinates.png"
    picture
        translate 150 * fade_between(smooth_step, 3, 4), 60, 0
        rotate_z 20 * fade_between(smooth_step, 4, 4)
        rotate_x 20 * fade_between(smooth_step, 5, 9)
        rotate_y (40 + 20 * sin time) * fade_between(smooth_step, 5, 9)


        line_color "#777"
        line_width 2
        line_arrow -600,    0, none, 600,   0, arrowhead
        line_arrow    0, -400, none,   0, 400, arrowhead
        locally
            rotate_x 90
            line_arrow    0, -400, none,   0, 400, arrowhead
        line_width 8
        line_arrow  590,    0, none, 600,   0, arrowhead
        line_arrow    0,  390, none,   0, 400, arrowhead
        locally
            rotate_x 90
            line_arrow    0, 390, none,   0, 400, arrowhead

        line_width 0
        font "Arial", 25
        color "black"
        align 0%
        vertical_align 100%
        text_box   65,   30, 120, 60, { text "(0, 0)" }
        text_box  555,   30, 120, 60, { text "(600, 0)" }
        text_box -525,   30, 120, 60, { text "(-600, 0)" }
        text_box   65, -350, 120, 60, { text "(-0, 400)" }
        text_box   65,  380, 120, 60, { text "(0, 400)" }

        at_step 1
        translate_z 50 * fade_at(smooth_step, 6)
        locally
            if smooth_step >= 7.5 then
                light 0
                light_position 300, 500, 1000
            extrude_depth 150 * fade_at(smooth_step, 7)
            color "orange"
            line_color "darkorange"
            rectangle -250, -100, 500, 200
            extrude_radius 10 * fade_at(smooth_step, 9)
            line_color "darkred"
            color "red"
            ellipse 200, 100, 500, 100
        style "code"
        color "white"
        align 50%
        vertical_align 50%
        text_box 200, 100, 500, 100,
            text "ellipse 200, 100, 500, 100"
        text_box -250, -100, 500, 200,
            text "rectangle -250, -100, 500, 200"


    picture
        only_at 1,
            text_box 300, -250, 400, 300,
                style "comment"
                paragraph "Shape coordinates are"
                paragraph { code "X, Y, Width, Height" }
                paragraph "(X,Y) is at shape center"
        only_at 2,
            text_box 300, 250, 400, 300,
                style "comment"
                text "Initially, coordinates are in pixel units and centered on the screen"
        only_at 3..4,
            text_box -100, 100, 400, 300,
                style "comment"
                paragraph "Translation and rotation change the coordinate system"
            text_box 450, -100, 400, 300,
                style "code"
                paragraph "translate 150, 0, 0"
                at_step 4
                paragraph "rotate_z 20"
        only_at 5,
            text_box -300, 200, 500, 300,
                style "comment"
                paragraph "The coordinate system is actually always in 3D with Z ""poking out"" of the screen"
            text_box 250, -100, 400, 300,
                style "code"
                paragraph "rotate_x 20"
                paragraph "rotate_y 40 + 20 * sin time"
        only_at 6,
            text_box -300, 200, 500, 300,
                style "comment"
                paragraph "3D transforms are possible even for 2D shapes"
            text_box 250, -100, 400, 300,
                style "code"
                paragraph "translate_z 50"
        only_at 7..9,
            text_box -300, 200, 400, 300,
                style "comment"
                paragraph "All 2D shapes can be extruded into 3D"
                at_step 8
                paragraph "and lighted"
            text_box 350, -100, 600, 300,
                style "code"
                if smooth_step >= 7.5 then
                    paragraph "light 0"
                    paragraph "light_position 300, 1000, 1000"
                paragraph "extrude_depth 150"
                at_step 9
                paragraph "extrude_radius 10"
        at_step 9


// ----------------------------------------------------------------------------
base_slide "Shapes",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tao3D offers a wealth of 2D and 3D shapes"
    illustration 35%, "Basic shapes.png"
    picture
        rotate_x 0.1 * mouse_y
        rotate_y -0.1 * mouse_x

        show_shape 0, "square", {square 0, 0, 100}
        show_shape 1, "circle", {circle 0, 0, 50}
        show_shape 2, "rectangle", {rectangle 0, 0, 200, 100}
        show_shape 3, "ellipse", {ellipse 0, 0, 200, 100}
        show_shape 4, "rounded_rectangle", {rounded_rectangle 0, 0, 200, 100, 40 + 30 * sin time}
        show_shape 5, "polygon", {polygon 0, 0, 200, 100, seconds mod 6 + 3}
        show_shape 6, "star_polygon", {star_polygon 0, 0, 150, 150, seconds mod 8 + 5, 5}
        show_3d_shape 7, "sphere", {sphere 0, 0, 0, 150}
        show_3d_shape 8, "cube", {cube 0, 0, 0, 200, 100, 80}
        show_3d_shape 9, "torus", {torus 0, 0, 0, 200, 50% + 30% * sin time}
        show_3d_shape 10, "cone", {cone 0, 0, 0, 100, 50, 300; cone 0, 0, 0, 100, -50, 300}
        show_3d_shape 11, "truncated_cone", {truncated_cone 0, 0, 0, 100, 50, 300, 50% + 30% * sin time; truncated_cone 0, 0, 0, 100, -50, 300, 50% + 30% * sin time}
        show_shape 12, "text",
            text_box 0, 0, 200, 100,
                font "Brush Script MT", 80
                align 50%
                text "Hello"
        show_shape 13, "path",
            path
                move_to 0, 0
                line_to 50, 0
                line_to 50, 50
                quad_to -200, 50, -50, -50
                quad_to -50, 100, 0, -50
        show_shape 14, "arrow",
            arrow        0,  30, 200, 50, 45, 30%
            double_arrow 0, -30, 200, 50, 45, 30%
        show_shape 15, "line",
            line_width 10 * sin(0.113 * time)^2
            line_color_hsv 30 * time, 0.9, 0.8
            line_arrow -100,  60, hollow_circle, 100,  60, hollow_square
            line_arrow -100,  30, none,          100,  30, none
            line_arrow -100,   0, square,        100,   0, arrowhead
            line_arrow -100, -30, diamond,       100, -30, rounded
            line_arrow -100, -60, circle,        100, -60, square


// ----------------------------------------------------------------------------
base_slide "Paths",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "A way to draw arbitrary shapes"
    illustration 60%, "Path.png"
    left_column
        * "Built from lines, quadrics, cubics"
        * "Arbitrary paths using code"
        * "Combine basic shapes..."
        * "... including text"

    right_picture
        example_box 1, "Path components", "path-1.ddd",
            import "path-1.ddd"
        example_box 2, "Arbitrary paths", "path-2.ddd",
            translate_y -250
            import "path-2.ddd"
        example_box 3, "Shapes in paths", "path-3.ddd",
            translate_y -250
            import "path-3.ddd"
        example_box 4, "Text in path", "path-4.ddd",
            import "path-4.ddd"


// ----------------------------------------------------------------------------
base_slide "Colors",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Several color models are available for convenience"
    illustration 15%, "Colors.png"
    picture
        attribute "Name", "color ""red""", { color "red" }
        attribute "RGB", "color 30%, 50%, sin time", { color 30%, 50%, sin time }
        attribute "HSV", "color_hsv 20*time, 50%, 100%", { color_hsv 20*time, 50%, 100% }
        attribute "Web hex", "color ""#30F0CA""", { color "#30F0CA" }
        attribute "Web RGB", "color ""rgb(200,30,10)""", { color "rgb(200,30,10)" }
        attribute "Web HSV", "color ""hsv(200,30,200)""", { color "hsv(200,30,200)" }
        attribute "Alpha", "color ""red"", sin time ^ 2", { color "red", sin time  ^ 2 }


// ----------------------------------------------------------------------------
base_slide "Shape attributes",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Attributes modify the drawing of shapes and are cumulative"
    illustration 40%, "Attributes.png"
    picture
        attribute "Line color", "line_color ""blue""",
            color "red"
            line_color "blue"
            line_width 2
        attribute "Line width", "line_width 10",
            color "red"
            line_color "blue"
            line_width 10
        attribute "Texture", "texture ""Attributes.png""",
            color_hsv 20 * time, 30%, 100%
            texture "Attributes.png"
        attribute "Transforms", "texture_transform { ... }",
            color "white"
            texture "Attributes.png"
            texture_wrap true, true
            texture_transform
                scale 0.3 + 3 * sin time
                rotate_z 25 * time
                scale_x 2 / 0.8
                translate -0.5, -0.5, 0
        attribute "Extrusion", "extrude_depth 80",
            color "red"
            line_color "blue"
            translate_z 50
            rotate_y 20 * time
            extrude_depth 80

        attribute "Gradients", "radial_gradient ...",
            color "white"
            radial_gradient 50, 50, 70, 200, 80,
                gradient_color   0%, "red",   100%
                gradient_color  50%, "white",  60%
                gradient_color 100%, "blue",   80%

        attribute "Blending", "blend_function ""one"", ""one""",
            color "red"
            circle 0, 0, 40

            color "white"
            texture_unit 0
            texture "Colors.png"
            texture_wrap true, true
            texture_transform
                scale 0.3 + 3 * sin time
                rotate_z -2 * time
                scale_x 2 / 0.8
                translate -0.5, -0.5, 0
            if seconds mod 2 = 0 then
                blend_function "src_alpha", "one_minus_src_alpha"
            else
                blend_function "one", "one"


// ----------------------------------------------------------------------------
base_slide "Using locally and shape",
// ----------------------------------------------------------------------------
    title
        text "Using "; code "locally"; text " and "; code "shape"
    subtitle "Making local changes to attributes and transforms"
    illustration 40%, "Locality.png"
    left_column
        * "Attributes and geometric transforms are cumulative"
        * "Changes can be kept local to a code block with "; code "locally"
        * "A shape can be made editable by putting inside a "; code "shape"
        * "The mechanisms governing that behavior are relatively subtle"
        §
        * "Recommended: edit only small blank documents, then copy-paste and replace "; code "shape"; text " with "; code "locally"

    right_picture
        example_box 1, "Cumulative attributes", "locally-1.ddd",
            import "locally-1.ddd"
        example_box 2, "Local changes", "locally-2.ddd",
            import "locally-2.ddd"
        example_box 3, "Editable shape", "locally-3.ddd",
            import "locally-3.ddd"
        example_box 4, "Multiple editable shapes", "locally-4.ddd",
            import "locally-4.ddd"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #3: Building a clock", 25%, "Clock.png",
// ----------------------------------------------------------------------------
    picture
        translate_y -200
        scale 80%
        import "demos/clock.ddd"


// ----------------------------------------------------------------------------
base_slide "3D objects",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Drawing complete 3D models"
    illustration 50%, "3D Shapes.png"
    picture
        translate_y 120

        light 0
        light_position 1000, 1000, 1000
        light_specular "white"

        rotate_x 0.1 * mouse_y
        rotate_y -0.1 * mouse_x + 180
        rotate_x -105
        object 0, 0, 0, 800, 800, 800, "models/Resurs3D_Audi_S5.obj"

    picture
        code_box 0, -200, 1300, 500, "Loading a 3D object",
            colorize load_text "demos/loading-object.ddd"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #4: 3D objects and lighting", 60%, "3D Shapes.png"
// ----------------------------------------------------------------------------



// ============================================================================
//
section_slide "Images, movies and textures",
//
// ============================================================================
    title page_label
    subtitle "So many ways to make things perty"
    picture
        translate 500, 400, 50
        rotate_z -5
        image 0, 0, 50%, 50%, "3D-TV.png"


// ----------------------------------------------------------------------------
base_slide "Images, pictures and drawings",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Loading images from files or URLs"
    illustration 25%, "3D Drawing.png"

    left_column
        * "All common image formats"
        * "Support for transparency"
        * "Load images from URLs"
        * "Images can be used as textures"
        * "Colorization"
        * "Texture dimensions"

    right_picture
        example_box 1, "Image from file", "picture-1.ddd",
            import "picture-1.ddd"
        example_box 2, "Transparent image", "picture-2.ddd",
            import "picture-2.ddd"
        example_box 3, "Image from URL", "picture-3.ddd",
            import "picture-3.ddd"
        example_box 4, "Image textures", "picture-4.ddd",
            import "picture-4.ddd"
        example_box 5, "Colorization", "picture-5.ddd",
            import "picture-5.ddd"
        example_box 6, "Texture dimensions", "picture-6.ddd",
            import "picture-6.ddd"


// ----------------------------------------------------------------------------
base_slide "Dynamic textures",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Generate textures using code"
    illustration 15%, "Dynamic Texture.png"

    left_column
        * "Generate texture with code"
        * "Accelerate rendering"
        * "Texturing effects"


    right_picture
        example_box 1, "Dynamic texture", "dynamic-1.ddd",
            import "dynamic-1.ddd"
        example_box 2, "Acceleration", "dynamic-2.ddd",
            import "dynamic-2.ddd"
        example_box 3, "Texturing effect", "dynamic-3.ddd",
            import "dynamic-3.ddd"



// ----------------------------------------------------------------------------
demo_slide "Live Coding #5: Dynamic textures", 30%, "Dynamic Texture.png"
// ----------------------------------------------------------------------------



// ============================================================================
//
section_slide "Text boxes and layouts",
//
// ============================================================================
    title page_label
    subtitle "Texts are a central element of way too many presentations"
    picture
        translate 500, 400, 50
        rotate_z -5
        locally
            radial_gradient 64, 64, 64, 128, 128,
                gradient_color   0%, "white", 80%
                gradient_color  80%, "white", 60%
                gradient_color 100%, "white",  0%
            ellipse 0, 0, 500, 400
        image 0, 0, 80%, 80%, "Text Layout.png"


// ----------------------------------------------------------------------------
base_slide "Text boxes and text layout",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Real-time text layout covering the majority of situations"
    illustration 35%, "Text Layout.png"

    left_column
        * "Text layout happens in "; code "text_box"
        * "Fonts, attributes, paragraphs"
        * "Horizontal and vertical alignment"
        * "Alignments are proportional"
        * "Justification is also proportional"
        * "Spread and last-line justification"
        * "Right-to-left support"
        §
        § "Sill quite a few bugs..."

    right_picture
        color "black", 10% * fade_between(smooth_step, 1, 7)
        line_color "lightgray", 90% * fade_between(smooth_step, 1, 7)
        rectangle 0, -200, 600, 300

        font "Arial", 45
        color "#400"
        line_color "transparent"

        example_box 1, "Text box", "text-1.ddd",
            translate_y -200
            import "text-1.ddd"
        example_box 2, "Fonts and attributes", "text-2.ddd",
            translate_y -200
            import "text-2.ddd"
        example_box 3, "Horizontal and vertical", "text-3.ddd",
            translate_y -200
            font "Arial", bold, 25
            import "text-3.ddd"
        example_box 4, "Proportional alignment", "text-4.ddd",
            translate_y -200
            import "text-4.ddd"
        example_box 5, "Justification", "text-5.ddd",
            translate_y -200
            font "Arial", 18
            import "text-5.ddd"
        example_box 6, "Spread", "text-6.ddd",
            translate_y -200
            font "Arial", 35
            import "text-6.ddd"
        example_box 7, "Right-to-left text layout", "text-7.ddd",
            translate_y -200
            font "Arial Unicode MS", 35
            import "text-7.ddd"


// ----------------------------------------------------------------------------
demo_slide "Live Coding #6: Slide layouts", 40%, "pinky-decor.png"
// ----------------------------------------------------------------------------


// Text layout
// Movies
// Slides
// Themes (using)
// Themes (creating)
// RegExp
// Shaders
// NodeJS



// ----------------------------------------------------------------------------
section_slide "Thank you",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Now is a good time for questions"

    picture
        translate 600, 500, 20
        rotate_z -8
        image 0, 0, 30%, 30%, "Tao3D.png"

    contents 0,
        text_box 500, -400, 600, 200,
            style "story"
            font_size 30
            anchor
                image -80, -40, 10% , 10% , "Tao3D.png"
            paragraph "This Tao3D presentation is available at"
            paragraph "https://github.com/c3d/tao3d-presentation"


disabled
    import SeasonsGreetingsTheme
    import BrightRectanglesTheme
    import PastelTrianglesTheme
    import WhiteChristmasTheme

    theme "SeasonsGreetings"
    slide "Tao3D is text based",
        subtitle "Easy to write your documents"
        § "Paragraphs for introductions"

        * "Bullets for key points"
        ** "Second-level bullets for details"
        ** "and additional information"

        * "A second bullet"
        ** "Make your points as fast as you type"
        ** "Use git to cooperate on presentations"
